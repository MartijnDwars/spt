module test5 

language Spoofax-Testing

setup [[[
	module x
  language TestingTesting
]]]

test Basic [[[
	test Testing TestingTesting [[
    module x
  ]]
]]]

test Builder [[[
	test builder [[
		module x
    entity Y {
    }
   ]] build generate-java fails
]]] 1 error /eXpected to/ /fail/
  

test Refactoring [[[
	test builder [[
		module x
    entity Y {
    }
  ]] build generate-java fails
]]] 1 error

test Simple runner [[[
  test runner [[
    module y
  ]] run debug to Module("y", [])
]]]

test Square brackets not allowed in test description [[[
  test the [] is bad!!
]]] parse fails

test Simple parse test [[[
  test parse [[
    module x
  ]] parse succeeds
]]]

test Simple parse test [[[
  test parse [[
    no module
  ]] parse to Module(_, _)
]]] 1 error

test Simple reference resolving [[[
  test resolve me not [[
    module x
    [[ entity y {} ]]
  ]] resolve
]]] 1 error


test More reference resolving [[[
  test resolve me not [[
    module x
    [[entity x {}]] [[entity y {}]]
  ]] resolve #3
]]] 1 error /selection/

test [[[
  test y [[
    module [[z]]
  ]] complete to "z" 1 error
]]] /combination of test conditions is not supported/


test Content completion [[[
  test [[
    module x
    entity Alpha {
      a : [[A]]
    }
  ]] complete to "Alpha"
]]]

test Two-digit errors [[[
  test [[
    module y
  ]] 10 errors
]]] parse succeeds

test Errors and parsing doesn't work together (1) [[[
  test [[
    module y
    entity Z { z : Q }
  ]] 1 errors parse to _
]]] /Combination of test conditions is not supported/

test Errors and parsing doesn't work together (2) [[[
  test [[
    module y
    entity Z { z : Q }
  ]] 1 errors 2 errors parse to _
]]] /Combination of test conditions is not supported/

test Errors and errors work together [[[
  test [[
    module y
    entity Z { z : Q }
  ]] 1 error 1 error
]]]

test Errors and warnings don't work together ... yet [[[
  test [[
    module y
    entity Z { z : Q }
  ]] 1 error 1 warning
]]] /Combination of test conditions is not supported/

test Reference resolving and completion don't work together (hides errors) [[[
  test [[
    module y
    entity [[Z]] { z : [[Z]] }
  ]] complete #2 to "Z"
     resolve #2 to #1
]]] /Combination of test conditions is not supported/

test Multiple completion conditions [[[
  test [[
    module y
    entity z { z : [[z]] }
  ]] complete to "z"
     complete to "String"
     complete to "Int"
]]] 0 warnings

test Test error at location [[[
  test Test [[
    module x
    entity A {
      a : [[B]]
    }
  ]] 1 error
]]]

test Test error at other location [[[
  test Test [[
    module x
    entity [[A]] {
      a : B
    }
  ]] 1 error
]]] 2 errors /marker expected at selection/

test Test error at only one location [[[
  test Test [[
    module x
    entity [[A]] {
      a : [[B]]
    }
  ]] 1 error
]]] /marker expected at selection/
