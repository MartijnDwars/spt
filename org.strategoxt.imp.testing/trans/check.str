module check

imports
  libstratego-lib
  libstratego-aterm
  include/Spoofax-Testing
  lib/editor-common.generated
  aterm-eq
  spoofax_testing
  file-resolving
  check-resolve
  check-complete

signature constructors
  error : Term * Term -> Term
  localized : Term * Term -> Term
  
rules
  check-note = fail
  
  check-error:
    Test(_, d, _, [_, _ | _]) -> "Specify only one success condition)" // maybe in the future...
  
  check-error:
    TestEmpty(_, d) -> "Empty test case"
  
  check-error:
    Test(_, _, _, expectations) -> $[No observer defined for [<Language>]]
    where
      not(spt-get-observer)
  
  check-warning:
    Test(_,description(d), i, expectations) -> (d, warnings)
    where
      input := <Test-get-input <+ !FailsParsing()>i
    ; if not(<?[_ | _]; all(is-parse-only-test)> expectations) then
        // TODO: set starting directory to project path using invoke-plugin-strategy
        plugin-strategy-invoke(|<Language>, "editor-init")
      ; input'    := (input, <project-path>, <InputFile>)
      ; messages  := <plugin-strategy-invoke(|<Language>, <spt-get-observer>)> input'
      ; messages' := <try(?Some(<id>)); try(\(t,e,w,n)->(e,w,n)\)>  messages
      else
        messages' := ([], [], [])
      end
    ; if !expectations => [] then
        warnings := <filter(check-expectation(|input, messages'))> [Success()]
      else
        warnings := <filter(check-expectation(|input, messages'))> expectations
      end
    ; not([])

  check-warning:
  	annotated(_, _) -> $[Matching with annotations not implemented(?)] // TODO: extend aterm-eq

  external plugin-strategy-invoke(|language, name)
  
  Test-get-input :
    file(f) -> <resolve-path(|<current-path>) ; read-text-file ; spt-parse-string>f
    
  Test-get-input :
    string(_,s,_){a*} -> parsed'
    where
      !a* => [parsed];
      if !parsed => Error(_) then
        parsed' := FailsParsing()
      else
        parsed' := parsed
      end
    <+
      parsed' := <spt-parse-string> s

  check-expectation(|ast, messages) :
    <not(Fails + FailsParsing)> -> parse-failed
    where
      if !ast => FailsParsing() + !ast => error(_, _) then
        parse-failed := <extract-parse-error>
      <+
        parse-failed := "Fails to parse"
      end
  
  extract-parse-error =
    ?error(_, [localized(<id>, _) | _])

  check-expectation(|ast, messages) :
    <not(Fails)> -> $[Observer fails: [details]]
    where
      !messages => Fail(details)

  check-expectation(|ast, messages) :
    _ -> $[Observer error: [details]]
    where
      !messages => Error(details)

  check-expectation(|ast, messages) :
    FailsParsing() -> $[Failure expected]
    where
      not(!ast => FailsParsing());
      not(!ast => error(_, _))

  check-expectation(|ast, messages) :
    pattern(p) -> $[Does not match pattern: [<write-to-string> ast]]
    where
      exploded := <explode-aterm> ast
    ; not(<aterm-eq> (exploded, <aterm-appl0-to-fun> p))

  check-expectation(|ast, messages) :
    file(f) -> $[Does not match pattern: [<write-to-string> ast]]
    where
      exploded := <explode-aterm> ast
    ; p := <resolve-path(|<current-path>); ReadFromFile; explode-aterm> f
    ; not(<aterm-eq> (exploded, <aterm-appl0-to-fun> p))

  check-expectation(|ast, messages) :
    Success() -> $[Unexpected message(s): [<grab-messages> messages]]
    where
      not(!messages => ([], [], []))

  check-expectation(|ast, messages) :
    Errors(n) -> $[[n] errors expected: [<grab-errors> messages]]
    where
      !messages => (errors, _, _)
    ; not(<eq> (<length> errors, <string-to-int> n))

  check-expectation(|ast, messages) :
    Error() -> $[1 error expected: [<grab-errors> messages]]
    where
      not(!messages => ([_], _, _))

  check-expectation(|ast, messages) :
    Warnings(n) -> $[[n] warning(s) expected: [<grab-warnings> messages]]
    where
      !messages => ([], warnings, _)
    ; not(<eq> (<length> warnings, <string-to-int> n))

  check-expectation(|ast, messages) :
    Warnings(n) -> $[No errors expected: [<grab-errors> messages]]
    where
      !messages => ([_ | _], _, _)

  check-expectation(|ast, messages) :
    Warning() -> <check-expectation(|ast, messages)> Warnings("1")

  check-expectation(|ast, messages) :
    Builds(x) -> <check-expectation(|ast, messages)> BuildsTo(x, wildcard())

  check-expectation(|ast, messages) :
    BuildsTo(_, _) -> $[Not implemented: [<write-to-string>]]

  check-expectation(|ast, messages) :
    BuildsFile(_, _) -> $[Not implemented: [<write-to-string>]]

  check-expectation(|ast, messages) :
    VarDecl(_, _) -> $[Not implemented: [<write-to-string>]]
    
  is-parse-only-test =
    SuccessParsing + FailsParsing

  grab-messages:
    (errors, warnings, notes) -> <write-to-string> // TODO: make nice string?
    where
      rules(GrabbedErrors   :+= errors);
      rules(GrabbedWarnings :+= warnings);
      rules(GrabbedNotes    :+= notes)

  grab-errors:
    (errors, _, _) -> <write-to-string> errors // TODO: make nice string?
    where
      rules(GrabbedErrors :+= errors)

  grab-warnings:
    (_, warnings, _) -> <write-to-string> warnings // TODO: make nice string?
    where
      rules(GrabbedWarnings :+= warnings)

signature constructors
  
  Fail : String -> Term
  Error : String -> Term
  fun : AFun -> ATerm

rules

  external plugin-strategy-evaluate(print-trace |language, strategy)

  check-warning:
    TestStratego(_, description(d), s, expectation) -> (d, warning)
    where
      plugin-strategy-invoke(|<Language>, "editor-init")
    ; s' := SDefT(d, [], [], s)
    ; result := <plugin-strategy-evaluate(<not(StrategyFails)> expectation
                                          |<Language>, s')> ()
    ; try(<grab-strategy-success-result(|d, result)> expectation)
    ; if !result => Error(error) then
        warning := error
      else
        warning := <check-expectation(|result)> expectation
      end

  check-expectation(|result) :
    StrategyFails() -> $[Failure expected: <pre>[<write-to-string> result']</pre>]
    where
      !result => Some(result')

  grab-strategy-success-result(|d, result) :
    StrategySuccess() -> StrategySuccess()
    where
      !result => Some(result')
    ; rules(GrabbedNotes :+= (d, $[Result: [<write-to-string> result'] (no => expectation specified)]))

  check-expectation(|result) :
    <not(StrategyFails)> -> $[Failure unexpected: <pre>[trace]</pre>]
    where
      !result => Fail(trace)

  check-expectation(|result) :
    StrategyEquals(p) -> $[Unexpected result: [<write-to-string> result']]
    where
      !result => Some(result')
    ; result'' := <explode-aterm> result'
    ; not(<aterm-eq> (result'', <aterm-appl0-to-fun> p))
    
  aterm-appl0-to-fun =
  	topdown(try(\appl(s, []) -> fun(s)\))
