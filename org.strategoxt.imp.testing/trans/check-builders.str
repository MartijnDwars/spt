module check-builders

imports
  libstratego-lib
  libstratego-aterm
  include/Spoofax-Testing
  lib/editor-common.generated
  aterm-eq
  spoofax_testing
  check
  check-resolve
  ast-compare

overlays
  
  OVERRIDE_INPUT(s) = Prim("\"SSL_EXT_overrideinput\"", [NoAnnoList(Str(s))])

rules
 
  spt-desugar:
    Run(x, arg, p) -> Build(x, arg, p)

  check-expectation(|ast, selections, messages) :
    c@Build(builder, arg, expected) ->
    <check-builder(|ast, selections, messages, c)> (builder, arg, expected)

  check-expectation(|ast, selections, messages) :
    c@Run(builder, arg, expected) ->
    <check-builder(|ast, selections, messages, c)> (builder, arg, expected)

  check-expectation(|ast, selections, messages) :
    c@Refactor(builder, arg, expected) ->
    <check-refactoring(|ast, selections, messages, c)> (builder, arg, expected)
  
  check-builder(|ast, selections, messages, condition):
    (builder, arg, expected) -> error
    with
      selected := <resolve-selection(|selections) <+ !ast> Numbered("1");
      input    := <get-service-input-term> selected;
      if !arg => Argument(arg') then
        plugin-strategy-evaluate(id |<Language>, OVERRIDE_INPUT(arg'))
      end;
      output := <execute-service(|input)> builder
    where
      error := <check-builder> (output, input, expected, condition)

  check-refactoring(|ast, selections, messages, condition):
    (builder, arg, expected) -> error
    with 
      (semnodes, _, options) := <spt-find-refactoring-description> builder;
      selected := <resolve-selection(|selections) <+ !ast> Numbered("1"); 
      selected' := <repeat(\_#([arg]) -> arg\)> selected; //Var(Id(x)) start looking for matching nodes from x
      user-input := <try(?Argument(<id>)); try(trim-chars(?'"'))> arg;
      if  
        input    := <get-service-input-term-refactoring> (selected', semnodes, options, user-input)
      then 
      	output := <execute-service(|input)> builder
      else 
      	output := Error("Selection is not matched with expected sort/constructor")
      end
    where
      error := <check-builder> (output, input, expected, condition)
	
  check-builder:
	(output, input, expected, condition) -> error
    where
      switch !output
        case ?Some(output'):
          with(result := <fetch-builder-result(|input, output')> condition);
          error       := <check-builder-expectation(|result)> expected
        case ?Error(m):
          if not(!condition => Fails()) then
            error := $[Unexpected error: [m]]
          end
        case ?Fail(m):
          error  := $[Unexpected error: [m]]
        otherwise: with(fail)
      end
  
  fetch-builder-result(|input, output):
    Run(_, _, _) -> output
  
  fetch-builder-result(|input, output):
    Build(_, _, _) -> result
    with
      !output => (<is-string>, result)
    <+
      !output => result
  
  fetch-builder-result(|input, output):
    Refactor(_, _, _) -> result
    with
      <?([(_,result)], _, _, _)> output
    <+
      !output => result
   
  check-builder-expectation(|result) :
    Fails() -> $[Builder was expected to fail, but produced: [result]]
   
  check-builder-expectation(|result) :
    File(_) -> $[Builder comparison to file is not implemented] // TODO
   
  check-builder-expectation(|result) :
    Fragment(_{^[ast | _]}) -> $[Unexpected output: [result']]
    where
      not(<eq> (ast, result) <+ <equal-in-desugared-form> (ast, result)) //TODO compare-ast
    with
      result' := <spt-pp-string> result
    <+
      result' := $[[<write-to-string> result] instead of [<write-to-string> ast]]
  
  equal-in-desugared-form:
  	(ast, result) -> (ast, result)
  	where
      input-observer := (ast, <InputFile>, <project-path>);
      ast-desugared := <plugin-strategy-invoke(|<Language>, <spt-get-observer>); try(?Some(<Fst>))> input-observer;
      <compare-desugared-asts> (ast-desugared, result) //TODO compare-ast with sugar aware diff function
  		
  check-builder-expectation(|result) :
    ATerm(p) -> $[Unexpected output: [<write-to-string> result]]
    where
      result' := <explode-aterm> result;
      not(<aterm-eq> (result', p))
