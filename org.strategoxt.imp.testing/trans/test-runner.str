module test-runner

imports
  libstratego-lib
  libstratego-aterm
  include/Spoofax-Testing
  editor-common.generated
  spoofax_testing
  check
  
signature constructors 
  True  : Boolean  
  False : Boolean

strategies
  find(s) =
  	abspath 
  ; string-replace(|"/./", "/") => base
  ; readdir
  ; filter(
      !$[[base]/[<id>]]
    ; if <isdir> <filemode> then
        find(s)
      else
        s
      end
    )
  ; flatten-list
  
  test-runner =
    testview-init 
  ; find-all-testsuites-in-project 
  ; map(testview-add-testsuite(|<get-testsuite-name-from-file>,<id>))
  ; map(try(run-testsuite))
  ; !None()
  
  find-all-testsuites-in-project =
    <find(has-extension(|"spt"))> "." 
  ; string-sort

  origin-offset =
    prim("SSL_EXT_origin_offset", <id>) => (<id>, _)
    
  run-testsuite :
    file -> (ast, [])
    where 
      ast  := <parse-spt-file> file
    ; editor-init
    ; ast' := <topdown(repeat(spt-desugar)); alltd(spt-desugar-refactoring-conditions)> ast 
    ; testcases := <collect-all(testview-add-testcase(|file, <test-to-description>,<origin-offset>))>ast'
    ; <map(run-testcase(|file))>testcases
  
  parse-spt-file = 
    read-text-file; parse-spt-string
      
  get-testsuite-name :
    testsuite(hds, _) -> name
    where name := <fetch-elem(?Name(<id>))>hds
  
  get-testsuite-name-from-file =
    parse-spt-file ; get-testsuite-name <+ id
    
  run-testcase(|testsuite) = 
    ?tst
  ; test-to-description => name
  ; testview-start-testcase(|testsuite, name)
  ; if errors := <check-warning>tst ; not((id,[])) then
      testview-finish-testcase(|testsuite, name, False())
    else
      testview-finish-testcase(|testsuite, name, True()) 
    end
    
  test-to-description = 
    ?Test(_, description(<id>), _, _)  
  + ?TestStratego(_, description(<id>), _, _)  
  + ?TestEmpty(_, description(<id>))

  external testview-init(|)
  external testview-add-testsuite(|testsuite, filename)
  external testview-add-testcase(|testsuite, description, linenr)
  external testview-start-testcase(|testsuite, description)
  external testview-finish-testcase(|testsuite, description, result)
  external parse-spt-string(|)
    
  